import com.android.build.gradle.BasePlugin
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application' version '8.1.1' apply false
    id 'com.android.library' version '8.1.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'com.diffplug.spotless' version '6.20.0' apply false
}

subprojects {
    plugins.withType(BasePlugin).configureEach {
        android {
            compileSdk 33
            defaultConfig {
                minSdk 21
            }
            // Can remove this once https://issuetracker.google.com/issues/260059413 is fixed.
            // https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions.jvmTarget = JvmTarget.JVM_1_8
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target('**/*.kt')
            ktlint('0.50.0').setEditorConfigPath("$rootDir/oauth2webview/.editorconfig")
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
